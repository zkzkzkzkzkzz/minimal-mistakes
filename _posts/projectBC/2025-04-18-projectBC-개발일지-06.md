---
title: "Project BC 개발일지 06"
excerpt: "이동 보간 및 셰이더 생성"

categories:
  - projectBC

permalink: /projectBC/dev-log-06/

toc: true
toc_sticky: true

date: 2025-04-18
last_modified_at: 2025-04-18
---

## 이동 보간

![moveSmooth](https://github.com/user-attachments/assets/11fa3bfe-11a0-4aca-9d95-9166ee187ed0)

이전 구조는 위치를 바로 정해주기 때문에 테스트 상황에서 일정 시간마다 순간이동으로 움직였다.\
실제 게임에서는 이동하는 과정이 보여야하기 때문에 보간을 추가하여 자연스럽게 보일 수 있도록 수정했다.

```cs
private IEnumerator MoveSmooth(Tile nextTile)
{
    Vector3 start = transform.position;
    Vector3 end = nextTile.transform.position + Vector3.up * unitYOffset;

    float dist = Vector3.Distance(start, end);
    float duration = dist / 2f;

    float elapsed = 0f;

    while (elapsed < duration)
    {
        elapsed += Time.deltaTime;
        float t = Mathf.Clamp01(elapsed / duration);
        transform.position = Vector3.Lerp(start, end, t);
        yield return null;
    }

    transform.position = end;
}
```

현재 고정된 이동속도는 이후 학생들의 고유 이동속도로 대체될 예정이다.

<br>

## 학생 SO 생성

Unit 클래스는 움직임같은 기본적인 공통 기능을 담고있는 클래스이기 때문에,\
각 학생들의 데이터를 저장하고 관리하기 위해 스크립트 오브젝트를 추가했다.

```cs
public enum Academy
{
    Millennium,
    Gehenna,
    Trinity,
}

public enum Club
{
    Seminar,
}

[CreateAssetMenu(fileName = "NewStudentData", menuName = "Unit/Student")]
public class StudentData : ScriptableObject
{
    [Header("기본 정보")]
    public string studentName;
    public Sprite icon;
    public GameObject prefab;
    public int starGrade;   // 1 ~ 3성

    [Header("스탯")]
    public int MaxHP;
    public int MaxMP;
    public int ATK;
    public int DEF;
    public float AtkSpeed;
    public float MoveSpeed;
    public float CritRate;
    public float CritDamage;

    [Header("시너지")]
    public Academy academy;
    public Club club;
}

```

스탯의 경우, TFT처럼 스킬 공격력(ap계수같은), 마법 저항력(스킬 방어력?)을 추가할까 고민했지만,\
블루 아카이브는 따로 구분하지 않고 공격력과 방어력이 스킬의 데미지도 함께 계산하기 때문에 따로 분류하진 않았다.

<br>

## 입 구현하기

![image](https://github.com/user-attachments/assets/7d81706b-7c31-4286-83d0-397b975db179)

[projectBC 개발일지 03](https://github.com/zkzkzkzkzkzz/zkzkzkzkzkzz.github.io/blob/master/_posts/2025-04-11-projectBC-%EA%B0%9C%EB%B0%9C%EC%9D%BC%EC%A7%80-03.md)
에서 언급했던 것처럼 블루 아카이브의 기본 모델은 입이 비어있다.


![image](https://github.com/user-attachments/assets/8578fccc-307d-4080-91ed-e5401907277b)

fbx는 다섯 개의 머테리얼로 구성되어 있는데, 머테리얼 이름대로 해당 부위를 담당하고 있다.\
그 중, 눈여겨 볼 것은 EyeMouth 머테리얼로, 바로 이 머테리얼이 눈과 입을 렌더하는 머테리얼이다.

### 입 구현 방식

<img src="https://github.com/user-attachments/assets/2980bae4-b942-4641-9954-8950cadb7813" width="48%">

블아 텍스처 중 입 모양을 모아둔 전용 텍스처가 존재한다.\
단순하게 말하면 여기서 원하는 입 모양을 골라 EyeMouth 텍스처 입 부분에 매핑하는 방식이다.

<br>

<p align="justify">

  <img src="https://github.com/user-attachments/assets/70e90b01-0ff3-47e3-97f8-91d2ebeba7cc" width="20%">

  <img src="https://github.com/user-attachments/assets/72dded7a-fe60-45cb-af8b-5dffc839e09a" width="20%">
  
</p>

왼쪽 텍스처는 입이 매핑될 원본 텍스처로 이름 또한 EyeMouth다.\
다만 잘보면 눈에 관련된 텍스처만 있는 것을 알 수 있는데,\
좌측 하단을 보면 하얀 사각형 구역이 있다. 바로 이 부분이 입에 해당한다.

저 하얀 사각형 구역에 입을 그대로 삽입하는 방식도 가능하겠지만,\
그렇게하면 입 모양을 상황에 따라 변경하기 힘들테니 입 마스크 텍스처(오른쪽 이미지)를 이용해\
셰이더에서 매핑하는 식으로 구현하고자 한다.

### 셰이더 구현

유니티에서 사용하는 셰이더는 과거 DirectX 자체엔진에서 사용한 셰이더와 달랐기 때문에 구현하기 앞서 유니티 셰이더를 공부할 필요가 있었다.\
[유니티 셰이더 공부](https://github.com/zkzkzkzkzkzz/zkzkzkzkzkzz.github.io/blob/master/_posts/graphics/2025-04-17-graphics-%EC%85%B0%EC%9D%B4%EB%8D%94.md)

#### 구현 코드

```hlsl
Shader "Custom/StudentFaceShader"
{
 Properties
    {
        _BaseTex("Base Texture", 2D) = "white" {}
        _MouthTex("Mouth Texture", 2D) = "white" {}
        _MouthMask("Mouth Mask", 2D) = "white" {}

        [IntRange]_Row("Row (U)", Range(0,7)) = 0
        [IntRange]_Col("Col (V)", Range(0,7)) = 0

        _Cutoff("Alpha Cutoff", Range(0,1)) = 0.5
    }

    SubShader
    {
        Tags { "RenderType"="Transparent" }
        LOD 200
        Cull Back

        Pass
        {
            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"

            sampler2D _BaseTex;
            sampler2D _MouthTex;
            sampler2D _MouthMask;
            float4 _BaseTex_ST;
            float4 _MouthMask_ST;

            int _Row;
            int _Col;
            float _Cutoff;

            struct appdata
            {
                float4 vertex : POSITION;
                float2 uv : TEXCOORD0;
            };

            struct v2f
            {
                float2 uv : TEXCOORD0;
                float4 vertex : SV_POSITION;
            };

            v2f vert(appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = v.uv;
                return o;
            }

            fixed4 frag(v2f i) : SV_Target
            {
                float2 uvBase = TRANSFORM_TEX(i.uv, _BaseTex);
                float2 uvMask = TRANSFORM_TEX(i.uv, _MouthMask);

                fixed4 baseColor = tex2D(_BaseTex, uvBase);
                fixed4 mouthMask = tex2D(_MouthMask, uvMask);

                fixed4 finalColor = baseColor;
                float alpha = 1.0;

                float2 mouthCellSize = 0.125;  // 8x8 그리드

                // 입 텍스처 좌상단 좌표
                float2 mouthOffset;
                mouthOffset.x = _Col * mouthCellSize;
                mouthOffset.y = 1.0 - (_Row + 1) * mouthCellSize;

                float2 mouthUV = (i.uv * 0.5) + mouthOffset;

                fixed4 mouthColor = tex2D(_MouthTex, mouthUV);

                float maskValue = saturate(mouthMask.r);
                float mouthAlpha = mouthColor.a * maskValue;

                finalColor = lerp(baseColor, mouthColor, mouthAlpha);
                alpha = (1.0 - maskValue) + mouthAlpha;

                clip(alpha - _Cutoff);
                return finalColor;
            }
            ENDHLSL
        }
    }
}
```

입 텍스처는 `128 * 128` 규격으로 일정하게 나눠져 있었기에, 타일맵을 구현하는 것처럼\
몇 행 몇 열인지 슬라이드로 정할 수 있도록 구현했다.

다만 현재 사용한 입 텍스처는 8x8 그리드 크기를 가지고 있었지만,\
다른 특정 학생의 입 텍스처의 경우, (예를 들어, 츠루기나 이즈나같은) 8x8 그리드 크기가 아니기 때문에 입이 이상한 위치에 그려지는 문제가 있다.\
이 부분은 별도의 전용 셰이더를 추가하거나 이미지 크기에 따라 계산을 다르게 적용하는 구조 변경이 필요할 것 같다.

렌더 타입은 알파 블렌딩을 사용하기 위해 Transparent를 사용했다.

---

```hlsl
// 입 텍스처 좌상단 좌표
float2 mouthOffset;
mouthOffset.x = _Col * mouthCellSize;
mouthOffset.y = 1.0 - (_Row + 1) * mouthCellSize;
```

행과 열을 계산하는 부분에서 신기했던 점은 유니티 UV 좌표계에서 (0, 0)이 좌상단이 아니라 좌하단이었던 점이다.\
유니티는 윈도우 환경에서 기본적으로 DirectX를 사용함에도 OpenGL과 동일한 UV 좌표계를 사용했다.

왜 OpenGL 좌표계를 사용한건지 이유를 찾아봤지만, 정확한 이유는 찾질 못했다.\
대신 여러 환경에서의 호환성과 블렌더같은 3d 프로그램이 OpenGL 좌표계를 사용하기 때문에 직관성이나 관례적인 이유로 해당 좌표계를 사용했다고 하는 것 같다.

---

```hlsl
float2 mouthUV = (i.uv * 0.5) + mouthOffset;
```

`0.5`는 매직넘버로 입력받은 UV를 그대로 사용하면 입 텍스처 전체가 매핑돼버렸다.\
여러 숫자를 넣어보며 그나마 자연스러운 수치를 찾아 하드코딩으로 우선 구현했다.\
하지만 다른 학생의 입 텍스처는 입 영역이 차이가 날 수도 있기 때문에, 정해진 값보단 가변적인 스케일 값으로 바꾸는 것이 좋을 것 같다.

#### 완성 예시

![image](https://github.com/user-attachments/assets/cef46309-117d-4fd3-9eb8-9b0acad027f3)

입이 작게 그려진 것인지 선명하지 않아서인지 거리가 조금만 멀어져도 입이 안 보이는 현상이 있다.\
개선이 필요할 듯하다.
